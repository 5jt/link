 msg←type SetFlags args;setconfig;lines;name;query;container;links;fn;current;link;file;exists;rc;config;flagarray;flags;i;ns;leaf;vfi;ok;z
 ⍝ Set or report on user and directory configuration settings
 ⎕IO ⎕ML←1 1
 setconfig←Config.GetConfig'' ⍝ Read user config
 :If 2=DEBUG←{6::0 ⋄ ⍵.Debug.debug}setconfig
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf

 type←⎕C type
 name←⊃args
 query←1=≢args
 lines←⍕1↓args
 :If (⍬≡2⊃args,⊂⍬)∨lines≡,'⍬' ⋄ lines←⍬
 :ElseIf ∧/⊃vfi←⎕VFI lines
     lines←2⊃vfi
 :Else
     →0⊣msg←'Invalid line numbers: ',lines
 :EndIf

 'Valid flag types are Stop and Trace'⎕SIGNAL((⊂type)∊'stop' 'trace')↓11

 :Trap DEBUG↓0
     container←⊃⎕RSI ⎕XSI U.ContainerNs ⍬
     fn←container⍎'⎕',type
     :If ~(container.⎕NC name)∊3 4
         →0⊣msg←'Not a function or operator: ',name
     :EndIf

     current←fn name
     msg←name,' ',(⍕current),(0=≢current)/'⍬'

     →query⍴0
     msg←'Was ',msg
     lines fn name ⍝ Do the deed
     lines←fn name ⍝ Read back in case some lines do not exist

     :Hold '⎕SE.Link.Links'
         (ns leaf)←U.SplitNs name

         :If 0∊⍴links←U.GetLinks
         :OrIf 9≠container.⎕NC ns
         :OrIf 0∊⍴link←links U.LookupRef container⍎ns
             →0
         :EndIf

         :If exists←⎕NEXISTS file←link.dir,'/.linkconfig'
             :If 0≠⊃(rc config)←Config.ReadConfigFile file
                 →0⊣msg←'ERRORS ENCOUNTERED: Unable to write flags to .linkconfig - ',config
             :EndIf
         :Else
             config←⎕NS''
         :EndIf

         flagarray←{0::⍬ ⋄ config.SourceFlags}⍬
         :If (≢flagarray)<i←({0::⍬ ⋄ flagarray.Name}⍬)⍳⊂name
             flagarray,←⎕NS ''
         :EndIf
         flags←i⊃flagarray
         type←(1∘⎕C)@1⊢type

         :If 0=≢lines
             ⎕EX 'flags.',type
         :Else
             ⍎'flags.',type,'←lines'
         :EndIf

         :If 0=≢flags.⎕nl -2 ⍝ All flags reset?
             flagarray←(i≠⍳≢flagarray)/flagarray
         :EndIf
         config.SourceFlags←flagarray
         config Config.WriteConfig file
     :EndHold

 :Else
     U.Resignal 1
 :EndTrap
