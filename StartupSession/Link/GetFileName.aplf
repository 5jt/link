 files←GetFileName items;container;file;i;link;name;nc;oldfile;parent;scal;shape;⎕IO
 ⍝ Get file names from APL names
 ⍝ returns empty string for unlinked names
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     ⎕IO←1
     container←⊃⎕RSI ⎕XSI U.ContainerNs ⍬
     :If U.HasLinks
         :Hold '⎕SE.Link.Notify'
             scal←1=≡,items ⋄ shape←⍴items←⊆items ⋄ files←(≢items←,items)⍴''
             :For i :In ⍳≢items
                 nc←container U.NameClass name←(⍕container),'.',i⊃items
                 (parent name)←U.SplitNs name
                 :If ~nc∊0 ¯1
                 :AndIf ~0∊⍴link←U.LookupRef parent←⍎parent
                     (file oldfile nc)←link(0 U.DetermineFileName)(⍕parent)name name ⍬  ⍝ U.NormNs parent
                     (i⊃files)←(~nc∊0 ¯1)/(1+0<≢oldfile)⊃file oldfile
                 :Else
                     (i⊃files)←''
                 :EndIf
             :EndFor
             files←⊃⍣scal⊢shape⍴files
         :EndHold ⍝ Notify
     :Else
         files←⊃⍣(1=≡,items)⊢(⍴⊆items)⍴⊂''
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
