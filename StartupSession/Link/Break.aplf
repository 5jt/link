 msg←Break nss;names;link
 ;ns;linked;broken;missing;mask;⎕IO;there;n;parents
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     :If ×⎕NC'⎕SE.Link.Links'
         ⎕SE.Link.Links/⍨←{0::1 ⋄ 0≠⎕NC ⎕SE.Link.Links.ns}⍬
     :AndIf ×≢⎕SE.Link.Links
         ⎕IO←1
         there←1⊃⎕RSI ⎕XSI U.ContainerNs ⍬         ⍝ reference to calling namespace
         :If 2<10|⎕DR nss
             nss←there{
                 ''≢0⍴⍵:⍵
                 9≠⍺.⎕NC ⍵::⍵
                 ⍺⍎⍵
             }¨nss
         :Else
             nss←there⍎nss
         :EndIf
         linked←⍎¨⎕SE.Link.Links.ns
         broken←0⍴⊂''
         missing←0⍴⊂''
         n←0
         :For ns :In nss
             mask←linked≠ns
             :If 0∊mask
                 link←(mask⍳0)⊃Links
                 :If 0≠link.⎕NC'fsw'
                     FileSystemWatcher.Break link.fsw
                 :EndIf
                 n+←U.RemoveFileLinksIn ns
                 ⎕SE.Link.Links/⍨←mask
                 broken,←⊂ns
             :Else
                 missing,←⊂ns
             :EndIf
         :EndFor
         msg←''
         :If ×≢broken
             msg,←⊂'Unlinked ',(⍕n),' items:',U.List broken
         :EndIf
         :If ×≢missing
             msg,←⊂'Not found:',U.List missing
         :EndIf
         :If ~0∊⍴msg ⋄ msg←⊃'; 'U.Join/msg ⋄ :EndIf
     :Else
         msg←'No active links'
     :EndIf
     :Trap 517 ⋄ {⎕SIGNAL ⍵}517 ⋄ :EndTrap ⍝ Flush association table
 :Else
     U.Resignal 1
 :EndTrap
