 {linked}←OnAfterFix(obj evt src ns oldname name file);nss;z;stop;trace;link;fullname;relname;nsref
 ⍝ Thin cover for editor onAfterFix event
 ⍝ Returns 1 if a link was found for the name, else 0
 ⍝   NB: if 0 is returned, no ⎕FX/⎕FIX was done
 ⍝ If obj is not linked, Link has no business here, but SALT might

 linked←0 ⍝ Let SALT have a go
 :If 0∊⍴nsref←#(0 U.GetRefTo)ns
 :OrIf 0∊⍴link←U.LookupRef nsref  ⍝ issue # 257: not a linked namespace
     :Return
 :EndIf

 :Trap 0
     :If (⎕NC fullname←(⍕ns),'.',name)∊3 4
         relname←(1+≢link.ns)↓fullname
     :AndIf ((⊂relname)∊link.flagged)∨0≠≢(stop←⎕STOP fullname),trace←⎕TRACE fullname
         z←'Stop'SetFlags (fullname stop) ns
         z←'Trace'SetFlags (fullname trace) ns
     :EndIf
 :Else
     :If 0≠{0::0 ⋄ ≢(⎕STOP fullname),⎕TRACE fullname}⍬
         ⎕←'Warning: unable to record Stop/Trace settings'
     :endIf
 :EndTrap

:Trap 501 ⍝ We do not want to crash inside the editor if Fix signals an error
     {}ns name oldname ⎕SE.Link.Fix ⍬ ⍝ on error, we'll bubble up now
 :Else
     U.Warn⊃⎕DMX.DM
 :EndTrap
