 msg←{opts}Resync ns;FixFile;diff;file;files;mask;new;ok;type;⎕IO;⎕ML;links;manual;z;names;name
 ⎕IO ⎕ML←1 1
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     opts←U.DefaultOpts⍎⎕NS∘⍬⍣(900⌶⍬)⊢'opts' ⍝ monadic?
     :If 0∊⍴links←U.GetLinks ⋄ →0⊣msg←'No active links' ⋄ :EndIf
     diff←0 4⍴⊂''
     :For ns :In links
         diff⍪←opts Diff ns  ⍝ will error if ns is not a linked namespace
     :EndFor

     manual←'?'=diff[;1] ⍝ Manual changes required?
     :If 0∊⍴diff         ⍝ nothing to do
         msg←'No action required' ⋄ →0

     :ElseIf ~opts.{0::0 ⋄ proceed}⍬
         msg←⊂(⍕≢diff),' update',(1<≢diff)↓'s required',(~∨/manual)/': use -proceed option to synchronise'
         msg,←(∨/manual)/⊂'NOTE: ',(⍕+/manual),' change',(1<+/manual)↓'s must be resolved by hand before -proceed is possible'
         msg,←(⊂''),↓⍕('Direction' 'Name' 'File' 'Comments'⍪diff)[;2 1 3 4]
         msg←↑msg
         →0
     :EndIf         

     :If ∧/mask←2≥type←'←→'⍳diff[;1]  ⍝ should be impossible due to checks above
         msg←''
         :If ∨/mask←type=1  ⍝ some APL objects to update from file
             z←{Notify'changed'⍵}¨diff[⍸mask;3]
             msg,←,(⍕+/mask),' file',(1<+/mask)↓'s read, '
         :EndIf
         :If ∨/mask←type=2  ⍝ some files to update from APL source
             z←Add names←diff[⍸mask;2]
             msg,←,(⍕+/mask),' file',(1<+/mask)↓'s updated, '
         :EndIf
         msg←¯2↓msg
     :Else  ⍝ cannot perform changes automatically
         U.Error'Some differences require manual resolution:',U.FmtLines↓⍕(~mask)⌿diff
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
