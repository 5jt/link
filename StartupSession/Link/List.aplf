 list←{opts}List ns
 ;nss;lineages;there;ref;infos;prop;L
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     :If ×⎕NC'⎕SE.Link.Links'
     :AndIf ×≢⎕SE.Link.Links
         list←↑⎕SE.Link.Links.(ns dir)
         :If ×≢ns
             there←⊃⌽2⍴⎕RSI↓⍨⎕XSI(∊⍨×¯1+⍳)⊂'⎕SE.UCMD'
             ref←9∊⎕NC'ns'
         :AndIf ref∨×there.⎕NC⍕ns
             ns←⍕there⍎⍣(~ref)⊢ns
             nss←⍕¨⎕SE.Link.Links.ns
             list⌿⍨←ns∘(⊃⍷)¨nss
         :EndIf
         infos←U.GetInfos ⍬
         :If ×≢infos
             lineages←{⍕¨{∪(⊃⍵).##,⍵}⍣≡2⊃⍵}¨infos
         :Else
             lineages←0⍴⊂0⍴⊂''
         :EndIf
         list,←+/lineages∘.∊⍨⊂¨⊣/list
         list⍪⍨←'Namespace' 'Directory' 'Items'
         :If ~900⌶⍬ ⍝ dyadic
         :AndIf 1≡⍎∘'extended'⍣(2∊⎕NC'opts.extended')⊢opts
             L←819⌶
             :For prop :In ('Case' 'Code')'Flatten'('Force' 'Extensions')('Force' 'Filenames')'Watch'
                 list,←(⊂↑prop),⎕SE.Link.Links⍎¨⊂L@1∊prop
             :EndFor
         :EndIf
     :Else
         list←'No active links'
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
