 name←{opts}StripCaseCode name
 ;path;ext;file;parts
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     :If 900⌶⍬
     :AndIf 0∊⍴opts←U.LookupFile name
         U.Error'Not linked: ',⍕name
     :EndIf
     :If name≡opts.dir ⋄ :Return ⋄ :EndIf  ⍝ would wrongfully cut name because of missing trailing slash
     (path file ext)←1 ⎕NPARTS name
     path←(≢opts.dir)↓path       ⍝ do not case-code linked directory
     parts←(U.Parts path),⊂file
     parts←U.StripCaseCode¨parts      ⍝ case-code each part of path independently
     name←⊃'/'U.Join/(⊂opts.dir),parts
     name,←ext
 :Else
     U.Resignal 1
 :EndTrap
