 msg←{opts}Configure args;setconfig;links;file;islinked;target;settings;allopts;exists;config;rc;p;names;values;json;container
 ⍝ Set or report on user and directory configuration settings
 ⎕IO ⎕ML←1 1
 target←⊃args
 settings←1↓args
 setconfig←Config.GetConfig'' ⍝ Read user config
 :If 2=DEBUG←{6::0 ⋄ ⍵.Debug.debug}setconfig
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     (allopts←⎕NS ⍬).(create)←0
     :If 0=⎕NC'opts' ⋄ opts←⎕NS''
     :ElseIf ''≡0⍴opts ⋄ opts←⎕SE.Dyalog.Array.Deserialise opts
     :EndIf

     islinked←0
     container←⊃⎕RSI ⎕XSI U.ContainerNs ⍬

     :If 9=container.⎕NC target
        target←⍕container⍎target
     :AndIf 0≠≢links←⎕SE.Link.Links
     :AndIf islinked←(⊂target)∊links.ns                  ⍝ If target is a linked namespace
         file←links[links.ns⍳⊂target].dir,'/.linkconfig'
     :ElseIf (,target)≡,'*'
         file←U.UserConfigFile
     :Else
         :If ⎕NEXISTS target
         :AndIf 1=1 ⎕NINFO target ⍝ target is a directory
             file←target,'/.linkconfig'
         :Else
             →END⊣msg←,⊂'ERRORS ENCOUNTERED: folder "',target,'" not found.'
         :EndIf
     :EndIf
     :If exists←⎕NEXISTS file
         :If 0≠⊃(rc config)←Config.ReadConfigFile file
             →END⊣msg←,⊂'ERRORS ENCOUNTERED: ',config
         :EndIf
     :Else
         config←⎕NS ''
     :EndIf

     'allopts' ⎕NS ⎕OR 'opts'

     :If 0=≢settings
         msg←⊂'Contents of "',file,'":'
         →END⊣msg,←'   '∘,¨Config.FormatOptions config
     :EndIf

     :If 0≠⊃(rc msg)←config Config.MergeSettings settings
         →END⊣msg←,⊂'ERRORS ENCOUNTERED: ',msg
     :EndIf

     json←1 ⎕JSON⍠('Dialect' 'JSON5')('HighRank' 'Split')('Compact' 0)⊢config
     (⊂json)⎕NPUT file 1
     msg←,⊂msg

 END:
      msg←1↓U.FmtLines msg
 :Else
     U.Resignal 1
 :EndTrap
