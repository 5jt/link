 msg←Export(ns dir)
 ;opts;outFail;numFail;nc
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     ns←⊃⌽⎕RSI ⎕XSI U.ContainerNs ns

     msg←⍕⍬⊤⍬
     :If 9.1≠nc←U.NameClass ns
         msg←'Source namespace not found: ',ns,' has name class ',(⍕nc)
     :Else
         opts←⎕NS ⍬
         opts.watch←'none'
         opts.source←'ns'
         opts.ns←U.NormNs ns
         opts.dir←dir←0 U.NormDir dir

         outFail←opts WriteFiles ns

         msg←'Exported: ',ns,' → ',dir
         numFail←≢outFail
         :If ×numFail
             msg,←'; ',(⍕numFail),' failed: ',1↓¯1↓1 ⎕JSON⍕¨outFail
         :EndIf
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
