 msg←Export(ns dir)
 ;opts;outFail;numFail;container;isChild;d
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     opts←⎕NS ⍬
     opts.watch←'none'
     opts.source←'ns'

     msg←⍕⍬⊤⍬
     (container ns)←⎕RSI ⎕XSI U.ContainerNs ns
     :If 0=⎕NC ns
         msg←'Source namespace not found: ',ns
     :Else
         isChild←U.IsChild ns
         dir↓⍨←-'/\'∊⍨⊃⌽dir
         :If ⎕NEXISTS dir
             dir←⊃⊃⎕NINFO⍠1⊢dir
         :Else
             dir←∊1 ⎕NPARTS dir ⍝ normalise name
         :EndIf

         dir←U.WinSlash dir

         outFail←opts WriteFiles(⍎ns)dir
         msg←'Exported: ',ns,' → ',dir
         numFail←≢outFail
         :If ×numFail
             msg,←'; ',(⍕numFail),' failed: ',1↓¯1↓1 ⎕JSON⍕¨outFail
         :EndIf
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
