 msg←{opts}Export(ns dir)
 ;outFail;numFail;nc
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     opts←U.DefaultOpts⍎⎕NS∘⍬⍣(900⌶⍬)⊢'opts' ⍝ monadic?
     ns←⊃⌽⎕RSI ⎕XSI U.ContainerNs ns

     :If ¯9≠nc←U.NameClass ns
         msg←'Source namespace not found: ',ns,' has name class ',(⍕nc)
     :Else
         opts.watch←'none'   ⍝ prevent tying to the files
         opts.source←'ns'
         opts.ns←ns←U.NormNs ns
         opts.dir←dir←0 U.NormDir dir

         outFail←opts U.WriteFiles ns dir

         msg←'Exported: ',ns,' → ',U.WinSlash dir
         numFail←≢outFail
         :If ×numFail
             msg,←'; ',(⍕numFail),' failed: ',1↓¯1↓1 ⎕JSON⍕¨outFail
         :EndIf
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
