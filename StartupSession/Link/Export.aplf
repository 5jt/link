 msg←{opts}Export(src dest);arrays;container;defopts;dir;failed;fixed;nc;ns;ok;rawsrc;single;⎕IO;⎕ML
 ⎕IO ⎕ML←1 1
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     rawsrc←,⍕src
     (container src)←⎕RSI ⎕XSI U.ContainerNs src

     (defopts←⎕NS ⍬).(overwrite arrays)←0 0
     opts←defopts U.DefaultOpts⍎⎕NS∘⍬⍣(900⌶⍬)⊢'opts' ⍝ monadic?
     :If ~opts.overwrite∊0 1 ⋄ U.Error'Invalid value ',(⍕opts.overwrite),' for modifier "overwrite" - must be one of: 0 1' ⋄ :EndIf
     :If ~(⊂opts.arrays)∊0 1 ⋄ opts.arrays←container U.NormName¨opts.arrays ⋄ :EndIf

     :If ¯1=nc←container U.NameClass src
         U.Error'Mal-formed namespace name: ',rawsrc
     :ElseIf 0=nc
         U.Error'Source not found: ',src
     :ElseIf U.Slash⊃⌽src
         U.Error'Trailing slash reserved for future extension'
     :Else
         :If single←¯9.1≠nc ⋄ :AndIf opts U.HasExtn dest  ⍝ dest is a file
             dir←⊃⎕NPARTS dest←0 U.NormFile dest
         :Else                                            ⍝ dest is a directory
             dir←dest←0 U.NormDir dest
         :EndIf
         :If single
             ns←⊃U.SplitNs src←container U.NormName src
             arrays←1   ⍝ always export single array
         :Else
             ns←src←U.NormNs src                      ⍝ src is an unscripted namespace
         :EndIf

         opts.watch←'none'   ⍝ prevent tying to the files
         opts.source←'ns'
         opts.ns←ns
         opts.dir←dir

         (fixed failed)←opts U.WriteFiles src dest opts.arrays opts.sysVars opts.overwrite

         :If ~single
             msg←⊂'Exported: ',ns,' → ',U.WinSlash dir
             :If ×≢failed ⋄ msg,←(⊂(⍕≢failed),' failed:'),failed ⋄ :EndIf
         :ElseIf ×≢failed ⋄ msg←⊂'Export failed: ',⊃failed
         :Else ⋄ msg←⊂'Exported: ',src,' → ',U.WinSlash fixed
         :EndIf
         msg←1↓U.FmtLines msg
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
