 {linked}←where Fix src;array;ext;file;files;link;name;nc;nosrc;ns;nss;oldfile;oldname;src;stop;z;⎕TRAP
⍝ Fix a function/operator or script, preserving any existing source files
⍝   Used internally by EditorFix "afterfix" processing
⍝   May be called by other tools providing the source in ⎕NR/⎕SRC format on the right
⍝   One day, the left argument of (ns name) may be inferred, for now it must be provided
⍝ Returns 1 if a link was found for the name, else 0
⍝   NB: if 0 is returned, no ⎕FX/⎕FIX was done

 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf

 :Trap DEBUG↓0
     →('⎕SE.Link.OnAfterFix'≡⊃1↓⎕XSI)/NOHOLD ⍝ Can't hold in a high-priority callback (Editor AfterFix callback)
     :Hold '⎕SE.Link.Notify'
NOHOLD:
         nosrc←0=≢src   ⍝ editor callback provides empty source
         (ns name oldname)←3↑where,'' ''
         oldname,←(0=≢oldname)/name ⍝ oldname not specified
         name,←(0=≢name)/oldname ⍝ name not specified
         ns←⍎⍣(''≡0⍴∊ns)⊢ns
         z←ns←⍬⍴ns

         (linked link)←0 ⍬

         :If 0≠⎕NC'⎕SE.Link.Links'
         :AndIf 0≠≢⎕SE.Link.Links
             nss←⍎¨⎕SE.Link.Links.ns
             :Repeat
                 :If ~linked←z∊nss
                     linked←(z←z⍎'##')∊nss
                 :EndIf
             :Until linked∨z∊# ⎕SE ⎕DMX
             :If linked
                 link←(nss⍳z)⊃⎕SE.Link.Links
             :EndIf

         :EndIf

         :If linked

             :If 'none' 'dir'∊⍨⊂link.watch ⍝ Not watching the workspace
             :AndIf 0=≢src
                 →0 ⍝ Just notification, probably editor callback. No more to do.
             :EndIf

             :Trap 0
                 (file oldfile)←1 U.DetermineFileName link ns name oldname src  ⍝ fix in target ns
             :Else ⋄ U.Resignal 1
             :EndTrap
             nc←ns.⎕NC⊂,name  ⍝ fixed by U.DetermineFileName

             :If (0≠≢oldfile)∧(oldname≢name)  ⍝ Repair damage done by editor on rename
                 (⊂ns.⎕NR oldname)⎕NPUT oldfile 1
                 2 ns.⎕FIX'file://',oldfile ⍝ Necessary with FSW?
                 file←∊(⊂name)@2 ⎕NPARTS file
             :ElseIf (0≠≢oldfile)∧(file≢oldfile)∧(name≡oldname)  ⍝ file name was changed by beforeWrite or forceFilename or forceExtensions
                 ⎕NDELETE oldfile    ⍝ old file must be removed
             :EndIf

             :If nosrc                       ⍝ Source not provided - find it
                 :Select ⌊nc
                 :Case 2
                     src←ns⍎name
                 :CaseList 3 4
                     src←ns.⎕NR name
                 :Case 9
                     src←⎕SRC ns⍎name
                 :Else
                     1 U.Resignal⍨' *** Fix: unable to handle ',(⍕ns),'.',name,' (⎕NC =',(⍕nc),')'
                 :EndSelect
             :EndIf

             :If 3=link.(⎕NC beforeWrite)
                 →((⍎link.beforeWrite)ns name oldname nc src file link 0)↓0
             :EndIf

             :If array←2=⌊nc
                 :Trap 11
                     src←,↓Serialise src
                 :Else
                     'Unsupported array'U.Resignal 1
                 :EndTrap
             :EndIf

             :Trap 0
                 3 ⎕MKDIR 1⊃⎕NPARTS file ⍝ make sure folder is there
                 (⊂src)⎕NPUT file 1
             :Else
                 1 U.Resignal⍨' *** Fix: Unable to write "',name,'" to file ',file
             :EndTrap

             :If ~(⊂link.watch)∊'both' 'dir'  ⍝ bind name to new file unless the filewatcher does it for us
                 :If ~array  ⍝ array must always pre-exist the call to ⎕SE.Link.Fix for now
                     :Trap 0
                         2 ns.⎕FIX'file://',file
                     :Else
                         1 U.Resignal⍨' *** Fix: Unable to re-fix "',name,'" from file ',file
                     :EndTrap
                 :EndIf
             :EndIf
         :EndIf
     :EndHold ⍝ 'Notify'
 :Else
     U.Resignal 1
 :EndTrap
