 {linked}←where Fix src;RSI;XSI;added;callback;delete;file;fix;link;name;nc;nosrc;ns;nsname;nsref;oldfile;oldname;this;update;watch;⎕IO
⍝ Fix a function/operator or script, preserving any existing source files
⍝   Used internally by EditorFix "afterfix" processing
⍝   May be called by other tools providing the source in ⎕NR/⎕SRC format on the right
⍝   One day, the left argument of (ns name) may be inferred, for now it must be provided
⍝ Returns 1 if a link was found for the name, else 0
⍝   NB: if 0 is returned, no ⎕FX/⎕FIX was done

 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf

 :Trap DEBUG↓0
     ⎕IO←1 ⋄ XSI←⎕XSI ⋄ RSI←⎕RSI
     added←'⎕SE.Link.Add'≡⊃1↓XSI  ⍝ user explicitly added the name to the link - update file independently of watch
     callback←∨/'⎕SE.Link.OnAfterFix' '⎕SE.SALTUtils.EditorFix'⍷⍨⊂⊃1↓XSI  ⍝ v18.0 started using ⎕SE.Link.OnFix/⎕SE.Link.OnAfterFix - v17.1 was called directly from SALT
     →callback/NOHOLD ⍝ Can't hold in a high-priority callback (Editor AfterFix callback)
     :Hold '⎕SE.Link.Notify'
NOHOLD:
         nosrc←0=≢src
         (ns name oldname)←3↑where,'' ''
         oldname,←(0=≢oldname)/name ⍝ oldname not specified
         name,←(0=≢name)/oldname ⍝ name not specified
         this←⊃RSI XSI U.ContainerNs ns
         nsname←⍕nsref←this(0 U.GetRefTo)ns

         :If 0∊⍴nsref ⋄ U.Error'Not a namespace: ',ns ⋄ :EndIf
         :If ~linked←~0∊⍴link←U.LookupRef ns←nsref ⋄ :Return ⋄ :EndIf

         (file oldfile nc)←link(0 U.DetermineFileName)nsname name oldname src  ⍝ fix in target ns

         :If nc=¯1 ⋄ U.Error'Invalid name: ',nsname,'.',name
         :ElseIf (0=nc)∧(~0∊⍴src) ⋄ U.Error'Invalid source for ',nsname,'.',name,': ',⍕src
         :ElseIf (0=nc) ⋄ U.Error'No source for ',nsname,'.',name
         :EndIf

         update←'ns'≡link.watch  ⍝ update files if watching namespace (not tied to file with 2 ⎕FIX)
         update∨←('both'≡link.watch)∧(2=⌊nc)∨(~⎕NEXISTS file)  ⍝ arrays need to update file even if watching both ways - new files too
         update∨←added  ⍝ explicit add must update file
         watch←'both' 'dir'∊⍨⊂link.watch  ⍝ watching files

         :If callback∧(0≠≢oldfile)∧(oldname≢name)  ⍝ Repair damage done by editor on rename
             (ns U.GetAplSource oldname)U.Into oldfile   ⍝ editor overwrites oldfile with new definition
             ns U.Untie name  ⍝ editor ties newname to oldfile
             :If ~watch  ⍝ FileSystemWatcher should do this for us
                 2 ns.⎕FIX'file://',oldfile
             :EndIf
             delete←0  ⍝ old file must be preserved
         :Else
             delete←(0≠≢oldfile)∧(file≢oldfile)∧(name≡oldname)  ⍝ old file must be removed : file name was changed by beforeWrite or forceFilename or forceExtensions
         :EndIf

         :If update<callback ⍝ Editor fix when not watching the workspace
             →0  ⍝ job done
         :EndIf

         :If nosrc    ⍝ Source not provided - find it
             src←ns U.GetAplSource name
         :EndIf

         :If 3=link.(⎕NC beforeWrite)  ⍝ user callback on file write
             →0 U.If~(⍎link.beforeWrite)'beforeWrite'link file(nsname,'.',name)nc(nsname,'.',oldname)src
         :EndIf

         fix←~nosrc ⍝ no need to update if source was read from APL
         fix∧←~callback ⍝ no need to refix if change was done through editor (in which case src was empty anyways)
         update∨←fix∧('both'≡link.watch)  ⍝ watching both requires 2 ⎕FIX which in turn requires updating the file

         :If update  ⍝ file system must reflect changes
             :Trap 0
                 :If nc=¯9  ⍝ tradns → folder
                     3 ⎕MKDIR file
                 :Else  ⍝ file
                     3 ⎕MKDIR 1⊃⎕NPARTS file ⍝ make sure folder is there
                     src U.Into file
                 :EndIf
             :Else
                 U.Error'Unable to write "',nsname,'.',name,'" to file: "',file,'"'
             :EndTrap
             fix∧←~watch  ⍝ filewatcher will tie the file for us
             :If delete  ⍝ old file must be removed
                 ⎕NDELETE oldfile
             :EndIf
         :EndIf

         :If fix
             :If 'both'≡link.watch ⋄ src←file ⋄ :EndIf   ⍝ tie to file with 2 ⎕FIX - see U.QFix
             :If ¯1 0∊⍨2⊃ns U.Fix name src
                 ##.U.Error'Unable to fix "',nsname,'.',name,'" from source: ',⍕src
             :EndIf
         :EndIf

     :EndHold ⍝ 'Notify'
 :Else
     U.Resignal 1
 :EndTrap
