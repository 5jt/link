 {res}←OnFix(obj evt src ns oldname name file)
 ⍝ Thin cover for editor onFix event

 ⍝ link issue #152: keep track of last fix to recover source as typed
 :If (name≢oldname)∧(0≠≢name)  ⍝ rename : get old source as typed before it disappears
     :If 0=ns.⎕NC oldname  ⍝ oldname did not exit (e.g. edit non-existing 'foo' and rename it to 'goo' before fixing
         src←0⍴⊂''
     :Else
         src←ns U.GetAplSource oldname  ⍝ when name is empty, it means that we create a new object that doesn't yet have a source
         :If 1=≡src ⋄ U.Error src ⋄ src←0⍴⊂'' ⋄ :EndIf   ⍝ failed to get source
     :EndIf
 :EndIf
 U.LASTFIX←src(⍕ns)oldname  ⍝ format nsref to avoid cross-references between # and ⎕SE

 res←1
