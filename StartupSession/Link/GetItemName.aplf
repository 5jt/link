 items←GetItemName files;actname;container;expname;link;nc;scal;shape;where;⎕IO;⎕ML
 ⍝ Get APL names from file names
 ⍝ returns empty string for unlinked files
 ⎕IO ⎕ML←1 1
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     container←⊃⎕RSI ⎕XSI U.ContainerNs ⍬
     :Hold '⎕SE.Link.Notify' '⎕SE.Link.Links'
         :If ~U.HasLinks 0 ⋄ items←⊃⍣(1=≡,files)⊢(⍴⊆files)⍴⊂'' ⋄ :Return ⋄ :EndIf
         scal←1=≡,files ⋄ shape←⍴files←⊆files ⋄ items←(≢files←,files)⍴''
         :For i :In ⍳≢files
             :If ~0∊⍴link←U.LookupFile file←i⊃files
                 link←⎕NS link ⋄ link.fastLoad←0  ⍝ force inspecting file contents
                 (where expname actname nc)←link U.DetermineAplName file
                 (i⊃items)←(~nc∊0 ¯1)/where,'.',actname
             :Else
                 (i⊃items)←''
             :EndIf
         :EndFor
         items←⊃⍣scal⊢shape⍴items
     :EndHold
 :Else
     U.Resignal 1
 :EndTrap
