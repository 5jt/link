 {r}←Expunge names
 ;container;refs;i;name;ref;src
⍝ Use in place of ⎕EX for linked names - will expunge source files where relevant
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0
     :Hold '⎕SE.Link.Notify'
         container←⊃⎕RSI ⎕XSI U.ContainerNs ⍬

         :If ×≢Links
             Links/⍨←0≠⎕NC⍕¨Links.ns
             refs←⍎¨Links.ns
             r←(≢names←,⊆names)⍴0
             :For i :In ⍳≢r
                 name←i⊃names
                 :If 9=⌊container U.NameClass name
                 :AndIf ~U.IsScripted ref←container U.Execute name
                     Links/⍨←refs≠ref
                     container.⎕EX name
                 :Else
                     src←container U.CurrentFileName name
                     r[i]←container.⎕EX name
                     :If (0≠≢src)∧0=≢container U.CurrentFileName name ⍝ There was source and now there is none
                         ⎕NDELETE src
                     :EndIf
                     ⍝ TODO : arrays
                 :EndIf
             :EndFor
         :Else
             container.⎕EX names
         :EndIf
     :EndHold ⍝ Notify

 :Else
     U.Resignal 1
 :EndTrap
