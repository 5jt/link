:Namespace FileSystemWatcher
    (⎕IO ⎕ML)←1 1

    ∇ NotifyEvent args
     ⍝ Process File System Watcher events, redirected via a timer

     :Select 2⊃args
     :Case 140  ⍝ Timer; do nothing for now
     :Case 7777 ⍝ Notify
         ⎕SE.Link.Notify 3⊃args
     :EndSelect
    ∇

    ∇ Notify nargs
     ⎕NQ 'QUEUE' 7777 nargs
    ∇
    
    ∇ WatchEvent(obj args);ct;nargs
     ⍝ Callback for System.IO.FileSystemWatcher instance
     ⍝ Passes info on to ⎕SE.Link.Notify for processing
      
      ⍝ {}2501⌶0 ⍝ kill thread on exit
      :If 83=⎕DR ct←args.ChangeType           ⍝ numeric: .NET Core Bridge
          nargs←(1 2 4 8⍳ct)⌷'created' 'deleted' 'changed' 'renamed' ''
      :Else                                   ⍝ object: .NET Framework Bridge
          nargs←⊂819⌶⍕ct
      :EndIf
      nargs,←⊂args.FullPath
      :If 0≠⎕NC⊂'args.OldFullPath'
          nargs,←⊂args.OldFullPath
      :EndIf
     
      Notify nargs
    ∇

    ∇ r←Watch args;⎕USING;path;filter;watcher;dotnetcore
     ⍝ args: path filter

      dotnetcore←(,'1')≡2 ⎕NQ '.' 'GetEnvironment' 'DYALOG_NETCORE'
      {z←⍵ ⎕WC'Timer'('Event'(140 7777)'NotifyEvent')('Interval' 10000)
      ⎕DQ ⍵}&'QUEUE' ⍝ Launch a thread to dequeue it
       ⍝ /// Need code somewhere to kill the thread and the timer again
      ⎕USING←',System',(~dotnetcore)/'.dll'
      watcher←⎕NEW System.IO.FileSystemWatcher
      watcher.(Path Filter)←args
      watcher.(onChanged onCreated onDeleted onRenamed)←⊂'WatchEvent'
      watcher.IncludeSubdirectories←1
      watcher.EnableRaisingEvents←1
     
      r←⎕NEW Disposable watcher ⍝ wrap a destructor around it
      r.⎕DF 1⌽'][',##.U.WinSlash⊃args
    ∇

    
    :Class Disposable
        :Field Public Object
        ∇ make ref
          :Access Public
          :Implements Constructor
          Object←ref
        ∇
        ∇ do_dispose ref
          :Trap 0
              ref.Dispose
          :EndTrap
        ∇
        ∇ dispose
          :Implements Destructor
          :Trap 90 ⍝ object may have been destroyed by )OFF
              Object.EnableRaisingEvents←0
          :EndTrap
          do_dispose Object
        ∇
    :EndClass
:EndNamespace
