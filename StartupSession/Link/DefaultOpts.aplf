 {opts}←{defopts}DefaultOpts opts;Default;nl

 :If ''≡0⍴opts ⋄ opts←Deserialise opts ⍝ pseudo array notation
 :Else ⋄ opts←⎕NS opts  ⍝ duplicate namespace to avoid changing caller's
 :EndIf

 Default←opts.{(⊆⍺){0=⎕NC ⍺:⍎⍺,'←⍵'}¨⊆⍵}

 'source'Default'dir'
 'watch'Default(1+U.CanWatch)⊃'ns' 'both'

 'flatten' 'caseCode' 'forceExtensions' 'forceFilenames'Default 0
 'fastLoad'Default 0

 'beforeWrite' 'beforeRead' 'customExtensions'Default''

 'codeExtensions'Default⊂'aplf' 'aplo' 'apln' 'aplc' 'apli' 'dyalog' 'apl' 'mipage'
 opts.codeExtensions{⍵↓¨⍨⍺=⊃¨⊆⍵}∘⊆⍨←'.'

 'typeExtensions'Default 0 2⍴0 ''
 opts.typeExtensions{_←⍺
     _⍪←2 'apla'
     _⍪←3 'aplf'
     _⍪←4 'aplo'
     _⍪←9.1 'apln'
     _⍪←9.4 'aplc'
     _⍪←9.5 'apli'
     _[∪⍳⍨⊣/_;]}←⍬

 :If ~900⌶⍬  ⍝ some more custom defaults
 :AndIf ~0∊⍴nl←defopts.⎕NL ¯2
     nl Default¨defopts⍎¨nl
 :EndIf
