 {opts}←{canwatch} DefaultOpts opts
 ;Default;⎕IO

 :If 0=⎕NC 'canwatch' ⋄ canwatch←0 ⋄ :EndIf
 :Trap DEBUG↓0
     ⎕IO←1

     opts←Deserialise⍣(''≡0⍴opts)⊢opts ⍝ pseudo array notation

     Default←opts.{(⊆⍺){0=⎕NC ⍺:⍎⍺,'←⍵'}¨⊆⍵}

     'source'Default'dir'
     'watch'Default(1+canwatch)⊃'ns' 'both'

     'flatten' 'caseCode' 'forceExtensions' 'forceFilenames'Default 0

     'beforeWrite' 'beforeRead' 'customExtensions'Default''

     'codeExtensions'Default⊂'aplf' 'aplo' 'apln' 'aplc' 'apli' 'dyalog' 'apl' 'mipage'
     opts.codeExtensions{⍵↓¨⍨⍺=⊃¨⊆⍵}∘⊆⍨←'.'

     'typeExtensions'Default 0 2⍴0 ''
     opts.typeExtensions{_←⍺
         _⍪←2 'apla'
         _⍪←3 'aplf'
         _⍪←4 'aplo'
         _⍪←9.1 'apln'
         _⍪←9.4 'aplc'
         _⍪←9.5 'apli'
         _[∪⍳⍨⊣/_;]}←⍬
 :Else
     U.Resignal 1
 :EndTrap
