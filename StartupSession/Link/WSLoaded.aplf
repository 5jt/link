 WSLoaded;i;keep;links;msg;wslinks
⍝ Action to be taken upon )LOAD or )CLEAR
 :Trap 0  ⍝ can't afford to error
     :Hold '⎕SE.Link.Links'

         ⍝ retrieve saved workspace links before the GetLinks which may overwrite Store 0
         :If U.HASSTORE ⋄ :AndIf ⎕NULL≢wslinks←U.Store 0
         :Else ⋄ wslinks←⍬
         :EndIf

         ⍝ preserve old links to ⎕SE - drop the rest
         :If ~0∊⍴links←U.GetLinks
         :AndIf 0∊keep←⎕SE=U.RootOf⍎¨links.ns
             ¯1 U.Log'Clearing # links:'(⍕(~keep)/links)
             Watcher.Break(~keep)/links
             links←keep/links
         :Else
             ¯1 U.Log'Nothing to clear'
         :EndIf

         ⍝ restore saved workspace links
         :If ~0∊⍴wslinks
             wslinks.restored←1
             links,←⎕SE.Link.⎕NS¨wslinks  ⍝ avoid cross-ref between # and ⎕SE
             msg←(⍕≢wslinks),' linked namespace',('s '/⍨1≠≢wslinks),' linked in this workspace: '
             'IMPORTANT' U.Warn (⊂msg),⍕¨wslinks.ns
             'IMPORTANT' U.Warn 'Link.Resync is required to use anything other than Link.Status and Link.Diff'
         :EndIf

         U.SetLinks links
     :EndHold
 :Else
     ⎕←'Link.WSLoaded: Unable to clear links (',⎕DMX.(Message{⍵,⍺,⍨': '/⍨×≢⍺}EM),')'
 :EndTrap
