 outFail←opts WriteFiles(ns dir);count;dir;files;i;inx;mask;names;nsref;ok;oldfiles;src;tradfiles;tradnames
 ⍝ Write items to file in folders
 ⍝ ns must be a fully specified traditional namespace
 ⍝ arrays not supported
 outFail←0⍴⊂''
 nsref←⍎ns

 ⍝ Create directory for namesapce
 3 ⎕MKDIR dir
 outFail,←(~⎕NEXISTS dir)/⊂ns  ⍝ can't create directory

 ⍝ Write children to files (except trad namespaces)
 :If 0≠≢names←nsref.⎕NL-U.NameClasses 0
     src←nsref U.GetAplSource¨names
     mask←0=≢¨src     ⍝ can't get source
     outFail,←(ns,'.')∘,¨mask/names
     (names src)←(~mask)∘/¨(names src) ⋄ count←≢names
     (files oldfiles)←2↑opts(1 U.DetermineFileName)(count/⊂ns)names names(count/⊂'')  ⍝ names are known to exist so don't provide source
     :If ∨/mask←(inx←⍳⍨819⌶¨files)≠(⍳≢files) ⍝ TODO should really test the whole list of files after recursion, in case beforeWrite messed up the names
         ⍝ cannot let user do this on any platform - if the directory is moved to windows then the user will be screwed
         names←names[,(mask/inx),[1.5]⍸mask]
         U.Error'File name case clash - try using caseCode←1:',⍕(ns,'.')∘,¨names
     :EndIf

     mask←src∊⎕NULL  ⍝ traditional namespaces
     (tradnames tradfiles)←mask∘/¨(names files)
     (files oldfiles names src)/⍨←⊂(~mask)
     :If 0≠≢files
         :If 3=opts.(⎕NC beforeWrite)  ⍝ user callback on file write - not on directories
             mask←(≢files)⍴1
             :For i :In ⍳⍴files
                 mask[i]←(⍎opts.beforeWrite)'beforeWrite'opts(i⊃files)(ns,'.',i⊃names)(nsref U.NameClass i⊃names)(ns,'.',i⊃names)src
             :EndFor
             (files oldfiles names src)/⍨←⊂mask  ⍝ keep what was not processed by user
         :EndIf
         mask←src U.Into¨files  ⍝ failed to write
         outFail,←(ns,'.')∘,¨(~mask)/names
         :If ∨/mask←mask∧(oldfiles≢¨files)∧(0≠≢¨oldfiles)  ⍝ delete oldfilename which was incorrect if write was successful
             ⎕NDELETE¨mask/oldfiles
         :EndIf
     :EndIf
     :If 0≠≢tradfiles ⍝ recurse on traditional namespaces
         tradnames←(ns,'.')∘,¨tradnames
         outFail,←⊃,/opts∘WriteFiles¨↓tradnames,[1.5]tradfiles
     :EndIf
 :EndIf
