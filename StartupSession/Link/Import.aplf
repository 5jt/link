 msg←{opts}Import(ns dir);defopts;inFail;nsref
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0

     (defopts←⎕NS ⍬).(overwrite)←0
     opts←defopts U.DefaultOpts⍎⎕NS∘⍬⍣(900⌶⍬)⊢'opts' ⍝ monadic?
     :If ~opts.overwrite∊0 1 ⋄ U.Error'Invalid value ',(⍕opts.overwrite),' for modifier "overwrite" - must be one of: 0 1' ⋄ :EndIf

     opts.watch←'none'  ⍝ prevent tying to the files
     opts.source←'dir'
     ns←⊃⌽⎕RSI ⎕XSI U.ContainerNs ns
     :If ~⎕NEXISTS dir
         U.Error'Source directory not found: ',dir
     :ElseIf 1≠1 ⎕NINFO dir
         U.Error'Not a directory: ',dir
     :ElseIf 0=≢nsref←(1 U.GetRefTo)ns ⍝ must be normal namespace
         U.Error'Unable to import to namespace: ',ns
     ⍝:ElseIf ~opts.overwrite
     ⍝:AndIf ~0∊⍴U.ListNames nsref  ⍝ some APL names defined
     ⍝    U.Error'Destination namespace not empty: ',ns,' - use overwrite modifier to force the import'
     :Else
         opts.ns←ns←U.NormNs ns
         opts.dir←dir←0 U.NormDir dir
         inFail←opts U.FixFiles nsref dir opts.overwrite
         msg←⊂'Imported: ',ns,' ← ',U.WinSlash dir
         :If ×≢inFail
             msg,←⊂(⊂(⍕≢inFail),' failed:'),inFail
         :EndIf
         msg←1↓U.FmtLines msg
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
