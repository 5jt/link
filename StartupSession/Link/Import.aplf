 msg←{opts}Import(ns dir)
 ;opts;nsref;inFail;numFail
 :If U.debug=2
     (1+⊃⎕LC)⎕STOP⊃⎕SI
 :EndIf
 :Trap DEBUG↓0

     opts←U.DefaultOpts⍎⎕NS∘⍬⍣(900⌶⍬)⊢'opts' ⍝ monadic?

     opts.watch←'none'  ⍝ prevent tying to the files
     opts.source←'dir'
     ns←⊃⌽⎕RSI ⎕XSI U.ContainerNs ns
     :If 0=≢nsref←(1 U.GetRefTo)ns ⍝ must be normal namespace
         U.Error'Unable to import to namespace: ',ns
     :ElseIf ~⎕NEXISTS dir
         U.Error'Source directory not found: ',dir
     :ElseIf 1≠1 ⎕NINFO dir
         U.Error'Not a directory: ',dir
     :Else
         opts.ns←ns←U.NormNs ns
         opts.dir←dir←0 U.NormDir dir
         inFail←opts U.FixFiles nsref dir
         msg←'Imported: ',ns,' ← ',U.WinSlash dir
         numFail←≢inFail
         :If ×numFail
             msg,←'; ',(⍕numFail),' failed: ',1↓¯1↓⎕JSON inFail
         :EndIf
     :EndIf
 :Else
     U.Resignal 1
 :EndTrap
